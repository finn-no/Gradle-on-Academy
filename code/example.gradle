allprojects {
    apply plugin: 'idea'

    group = 'no.finn.gradle'
    version = calculateVersion()

    plugins.withType(JavaPlugin) {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
    }
}

ext.versions = [
    slf4j: '1.7.5',
    logback: '1.0.13',
    spock: '0.7-groovy-2.0'
]

ext.libs = [
    testing: ['junit:junit:4.11', "org.spockframework:spock-core:${versions.spock}"]
]

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'findbugs'

    repositories {
        maven {
            url = 'http://mavenproxy.finntech.no/finntech-release/'
        }
    }

    configurations {
        moduleTestCompile
        moduleTestCompile.extendsFrom testCompile

        compile.transitive = false
        testCompile.transitive = false

        all*.exclude group: 'commons-logging'
        all*.resolutionStrategy {
            force "org.slf4j:slf4j-api:${versions.slf4j}"
        }
    }

    sourceSets {
        test {
            java {
                exclude '**/module/**', '**/cucumber/**'
            }
            groovy {
                exclude '**/module/**', '**/cucumber/**'
            }
            resources {
                exclude '**/cucumber/**'
            }
        }

        moduleTest {
            java {
                srcDir 'src/test/java'
                include '**/module/**/*.java'
            }
            groovy {
                srcDir 'src/test/groovy'
                include '**/module/**/*.groovy'
            }
            resources {
                srcDir 'src/test/resources'
            }
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${versions.slf4j}"

        testCompile libs.testing
    }

    findbugs {
        reportLevel = 'high'
    }
}

def calculateVersion() {
    def gitDescribe = "git describe".execute()
    int rc = gitDescribe.waitFor()

    if (rc != 0) {
        throw new IllegalStateException("Could not execute 'git describe': ${gitDescribe.err.text}")
    }

    def (version, extra) = gitDescribe.in.text.tokenize('-')

    if (extra) {
        "${version}-SNAPSHOT"
    } else {
        version.trim()
    }
}
